plugins {
	id 'org.hidetake.ssh' version '2.7.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'application'

mainClassName = "app.SimpleWebServer"

repositories {
    maven {
        url "https://maven.csse.rose-hulman.edu/artifactory/libs-release"
    }
}

dependencies {
	testCompile 'junit:junit:4.+'
	compile group: 'com.google.http-client', name: 'google-http-client', version: '1.22.0'
	compile group: 'com.google.http-client', name: 'google-http-client-jackson', version: '1.22.0'
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
  	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


group = "edu.rosehulman.csse477.buildswebservers4fun"

if (System.getenv("CI_BUILD_TAG") != null) {
    version = "${System.getenv("CI_BUILD_TAG")}"
} else {
    version = '1.0.0-SNAPSHOT'
}


jar {
    manifest {
        attributes "Implementation-Title": "Webserver3000",
                "Implementation-Version": "${version}",
                'Main-Class': 'app.SimpleWebServer'
    }
}


task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'app.SimpleWebServer'
    }
    baseName = 'WebServer'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task prod(dependsOn: [clean, build, test, fatJar]) {
    build.mustRunAfter clean
    fatJar.mustRunAfter test
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "file://${buildDir}/repo")
		}
	}
}

task packageSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
			artifact packageSources
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASSWORD')
            }
            url "https://maven.csse.rose-hulman.edu/artifactory/student-release-local"
        }
    }
}

// Code coverage analyzer
jacoco {
	toolVersion = "0.7.+"
}

jacocoTestReport {
	group = "Reporting"
	description = "Generate Jacoco coverage reports after running tests."
	additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
	
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled true
	}
}


test {
	jacoco {
		append = false
	}
}

test.finalizedBy(jacocoTestReport)

task codeCoverageInfo (dependsOn: jacocoTestReport) {
	ext.srcFile = file("${reportsDir}/jacoco/test/jacocoTestReport.xml")
	doLast {
		println "Transforming source file."
		
		def parser = new XmlParser()
		parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false);
		parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
		parser.setFeature("http://xml.org/sax/features/namespaces", false)
				  
		def reportNode = parser.parse(srcFile)
		reportNode.children().each {
			if(it.name() == 'counter') {
				String type = it.@type
				float missed = it.@missed as float
				float covered = it.@covered as float
				float percentage = covered / (missed + covered) * 100
				println "Code Coverage[$type]: $percentage%"
			}
		}
	}
}
jacocoTestReport.finalizedBy(codeCoverageInfo)

// SSH Publishing
ssh.settings { knownHosts = allowAnyHosts }
remotes {
	prod {
		host = '477-38.csse.rose-hulman.edu'
		user = System.getenv('SSH_USER')
		password = System.getenv('SSH_PASSWORD')
	}
	test {
		host = '477-33.csse.rose-hulman.edu'
		user = System.getenv('SSH_USER')
		password = System.getenv('SSH_PASSWORD')
	}
}
task publishToTest(dependsOn: distZip) << {
	ssh.run {
		session(remotes.test) {
			println "Checking if remote /tmp/rosecalculator exists ..."
			def result = execute('if test -d /tmp/rosecalculator; then echo "exists"; fi')						
			if(result != "exists") {
				println("Remote /tmp/rosecalculator does not exist, creating a new one...")
				execute('mkdir -p /tmp/rosecalculator')
				println("Remote /tmp/rosecalculator created!")
			}
			println "Transfering file from local build/distributions to remote /tmp/rosecalculator ..."
			put from: "${buildDir}/distributions/*", into: '/tmp/rosecalculator'
			println "Files transfer complete now setting up the web server ..."
			
			println "Checking if remote /var/www/html/rosecalculator exists ..."
			result = execute('if test -d /var/www/html/rosecalculator; then echo "exists"; fi')
			if(result != "exists") {
				println("Remote /var/www/html/rosecalculator does not exist, creating a new one...")
				executeSudo('mkdir -p /var/www/html/rosecalculator', pty: true)
				println("Remote /var/www/html/rosecalculator created!")
			}
			println "Copying data from the temp folder to the web folder ..."
			executeSudo('cp /tmp/rosecalculator/* /var/www/html/rosecalculator', pty: true)
			println "Done! Clearing the tmp folder ..."
			execute('rm -rf /tmp/rosecalculator')
			println "All done!" 
			println "Rosecalcultor has been deployed to http://477-33.csse.rose-hulman.edu/rosecalculator/"
		}
	}
}

task publishToProd(dependsOn: distZip) << {
	ssh.run {
		session(remotes.prod) {
			println "Checking if remote /tmp/rosecalculator exists ..."
			def result = execute('if test -d /tmp/rosecalculator; then echo "exists"; fi')						
			if(result != "exists") {
				println("Remote /tmp/rosecalculator does not exist, creating a new one...")
				execute('mkdir -p /tmp/rosecalculator')
				println("Remote /tmp/rosecalculator created!")
			}
			println "Transfering file from local build/distributions to remote /tmp/rosecalculator ..."
			put from: "${buildDir}/distributions/*", into: '/tmp/rosecalculator'
			println "Files transfer complete now setting up the web server ..."
			
			println "Checking if remote /var/www/html/rosecalculator exists ..."
			result = execute('if test -d /var/www/html/rosecalculator; then echo "exists"; fi')
			if(result != "exists") {
				println("Remote /var/www/html/rosecalculator does not exist, creating a new one...")
				executeSudo('mkdir -p /var/www/html/rosecalculator', pty: true)
				println("Remote /var/www/html/rosecalculator created!")
			}
			println "Copying data from the temp folder to the web folder ..."
			executeSudo('cp /tmp/rosecalculator/* /var/www/html/rosecalculator', pty: true)
			println "Done! Clearing the tmp folder ..."
			execute('rm -rf /tmp/rosecalculator')
			println "All done!" 
			println "Rosecalcultor has been deployed to http://477-38.csse.rose-hulman.edu/rosecalculator/"
		}
	}
}